plugins {
    id 'org.jenkins-ci.jpi' version '0.22.0'
    id 'net.researchgate.release' version '2.6.0'
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.50"
    id 'java'
    id 'idea'
}

group = 'org.jenkins-ci.plugins'
version = '2.5.1'
description = """This plugin integrates Jenkins users with Selenium Testing on CrossBrowserTesting.com. CrossBrowserTesting.com provides cross browser testing of websites, webpages, and web applications on Windows, Macs, and real iPhones, iPads, and Android Phones and Tablets."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
/*
 * gradle-jpi
 * https://github.com/jenkinsci/gradle-jpi-plugin/blob/master/README.md
*/
jenkinsPlugin {
    coreVersion = '2.5'
    displayName = 'Lambdatest Plugin'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/CrossBrowserTesting+Plugin'
    gitHubUrl = 'http://github.com/jenkinsci/crossbrowsertesting-plugin'
    repoUrl = 'https://repo.jenkins-ci.org/releases'
    fileExtension = 'hpi'

    developers {
        developer {
            id 'Lambdatest'
            name 'Lambdatest Pvt Ltd'
            email 'support@lambdatest.com'
        }
    }
    licenses {
        license {
            name 'MIT License'
            url 'http://opensource.org/licenses/MIT'
        }
    }
}
/*
 * gradle-release
 * https://github.com/researchgate/gradle-release/blob/master/README.md
 */
release {
    failOnSnapshotDependencies = false
    failOnUnversionedFiles = false
    tagTemplate = '$name-$version'
    buildTasks = ['build']
    scmAdapters = [net.researchgate.release.GitAdapter]

    git {
        requireBranch = 'master'
        pushToRemote = 'jenkins'
        commitVersionFileOnly = false
        signTag = false
    }
}

dependencies {
   
    compile group: 'org.json', name: 'json', version:'20151123'
    compile group: 'commons-codec', name: 'commons-codec', version:'1.9'
    testCompile group: 'org.codehaus.mojo', name: 'findbugs-maven-plugin', version: '3.0.4'
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    compile group: 'com.fizzed', name: 'jne', version: '3.0.1'

    jenkinsPlugins group: 'org.jenkins-ci.plugins', name: 'junit', version: '1.9', ext: 'jar'
    jenkinsPlugins group: 'org.jenkins-ci.plugins', name: 'credentials', version: '2.1.12', ext: 'jar'



    jenkinsPlugins group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '2.12', ext: 'jar'
}

/*
 * specify a Jenkins version
 * otherwise uses what is specified in 'jenkinsPlugin.coreVersion'
 * -Pjenkins=${jenkins_version}
 */
if (project.hasProperty('jenkins')) {
    jenkinsPlugin.coreVersion = project.getProperty('jenkins')
}
task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
}
task changelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("CHANGELOG.md");
    templateContent = """

{{#tags}}
## {{name}}
{{#issues}}
### {{title}}
{{#commits}}
  {{#hash}}
  {{#message}}
   * [{{hash}}](https://github.com/jenkinsci/crossbrowsertesting-plugin/commit/{{hash}}) {{{message}}}
  {{/message}}
  {{/hash}}
{{/commits}}
{{/issues}}
{{/tags}}
 """;
}
